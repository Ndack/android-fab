buildscript {
	ext.kotlin_version = '1.2.60'

	repositories {
		jcenter()
		google()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:3.1.3'
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

allprojects {
	repositories {
		jcenter()
		maven {
			url "https://maven.google.com"
		}
	}
}

@SuppressWarnings("GroovyUnusedDeclaration")
static def getSecrets() {
	try {
		// load in default file
		Properties secrets = new Properties()
		secrets.load(new FileInputStream(new File('secrets.properties')))

		// fix keystore path
		secrets['KEYSTORE_PATH'] = new File('.').absolutePath + '/' + secrets['KEYSTORE_PATH']

		return secrets
	} catch (Exception ignored) {
		System.out.println("Warning: secrets file not loaded")
		return new Properties()
	}
}

@SuppressWarnings("GroovyUnusedDeclaration")
static def getProps() {
	Properties properties = new Properties()
	properties['version_code'] = 20
	properties['version_name'] = '2.3.1'
	return properties
}
